if(NOT WIN32)
    string(ASCII 27 Esc)
    set(ColorReset "${Esc}[m")
    set(ColorBold "${Esc}[1m")
    set(Red "${Esc}[31m")
    set(Green "${Esc}[32m")
    set(Yellow "${Esc}[33m")
    set(Blue "${Esc}[34m")
    set(Magenta "${Esc}[35m")
    set(Cyan "${Esc}[36m")
    set(White "${Esc}[37m")
    set(BoldRed "${Esc}[1;31m")
    set(BoldGreen "${Esc}[1;32m")
    set(BoldYellow "${Esc}[1;33m")
    set(BoldBlue "${Esc}[1;34m")
    set(BoldMagenta "${Esc}[1;35m")
    set(BoldCyan "${Esc}[1;36m")
    set(BoldWhite "${Esc}[1;37m")
endif()

# see https://github.com/STEllAR-GROUP/hpx/blob/master/cmake/HPX_Message.cmake
function(_to_string var)
    set(_var "")

    foreach(_arg ${ARGN})
        string(REPLACE "\\" "/" _arg ${_arg})

        if("${_var}" STREQUAL "")
            set(_var "${_arg}")
        else()
            set(_var "${_var} ${_arg}")
        endif()
    endforeach()

    set(${var}
        ${_var}
        PARENT_SCOPE)
endfunction()

function(krr_info)
    set(msg)
    _to_string(msg ${ARGN})
    message(STATUS "[${Green}INFO${ColorReset}] KiraraProject: ${msg}")
    unset(args)
endfunction()

function(krr_debug)
    set(msg)
    _to_string(msg ${ARGN})
    message(DEBUG "[DEBUG] ${BoldWhite}KiraraProject${ColorReset}: ${msg}")
endfunction()

function(krr_warn)
    set(msg)
    _to_string(msg ${ARGN})
    message(WARNING "[${BoldYellow}WARN${ColorReset}] KiraraProject: ${msg}")
endfunction()

function(krr_error)
    set(msg)
    _to_string(msg ${ARGN})
    message(FATAL_ERROR "[${BoldRed}ERROR${ColorReset}] KiraraProject: ${msg}")
endfunction()

function(krr_message level)
    if("${level}" MATCHES "ERROR|error|Error")
        krr_error(${ARGN})
    elseif("${level}" MATCHES "WARN|warn|Warn")
        krr_warn(${ARGN})
    elseif("${level}" MATCHES "DEBUG|debug|Debug")
        krr_debug(${ARGN})
    elseif("${level}" MATCHES "INFO|info|Info")
        krr_info(${ARGN})
    else()
        krr_error("message" "\"${level}\" is not an KRR configuration logging level.")
    endif()
endfunction()
